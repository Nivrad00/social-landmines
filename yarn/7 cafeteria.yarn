title: Start
tags: 
colorID: 0
position: 177,-207
---
<<show background cafeteria>>
You find your way to your usual seat in the cafeteria, next to your friend Juana.

<<show juana laugh center>>
{$player}: Hey!
Juana: Yo, how was your weekend?
{$player}: Good, how was yours?
<<show juana smile center>>
Juana: Pretty good, I didn't do much.

<<show grant smirk left>>
<<show juana frown center>>
Grant: Oh my God.
<<show brad smirk right>>
Brad: What?

<<set $mood to $mood + $picked_on>>
<<if $category1 == "popularity">> 
    Grant: That party last week was so awesome. EVERYONE was there. 
    Grant: {$player}, I didn't see you there, too bad.
    <<show brad laugh right>>
    Brad: Ha, {$player} would never be invited.

<<elseif $category1 == "partner">> 
    Grant: Did you see {$player}'s {$passion1} with that hot kid from Western the other night?
    <<show brad laugh right>>
    Brad: Yeah, I saw them kissing last week.
    Grant: I don't think {$they} even knew {$their} {$passion1} is cheating...

<<elseif $category1 == "friendships">> 
    Grant: {$player} is sitting next to someone! I didn't even know {$they} had friends.
    <<show brad laugh right>>
    Brad: Yeah, I know, right?

<<elseif $passion1 == "academics">>
    Grant: Did you see that {$player} failed that test last week?
    Brad: Nice going, loser.
<<elseif $passion1 == "sports">>
    Grant: Did you see that {$player} totally blew it at {$their} game last week?
    Brad: Nice going, loser.
<<elseif $passion1 == "theater">>
    Grant: Did you see how much {$player} sucked at {$their} performance last week?
    Brad: Nice going, loser.
<<elseif $passion1 == "arts">>
    Grant: Did you see {$player}'s crappy work at that art show last week?
    Brad: Nice going, loser.
<<endif>>

[[Ignore them|Ignore]]
[[Say something back|Say something]]
===
title: Ignore
tags: 
colorID: 0
position: 49,-451
---
<<show grant angry left>>
Grant: Oh, so you're just going to ignore us? 
Brad: I told you {$they} {$are} stupid.
You keep ignoring them, poking at your food instead. Juana looks concerned, but stays silent.
[[End lunch]]
===
title: Say something
tags: 
colorID: 0
position: 295,-449
---
<<if $category1 == "popularity">> 
    {$player}: Whatever, I was busy anyways.
    Brad: Yeah, keep telling yourself that.

<<elseif $category1 == "partner">> 
    {$player}: Leave my {$passion1} out of it!
    Brad: Sorry, did we strike a nerve?

<<elseif $category1 == "friendships">> 
    {$player}: Shows what you know.
    Brad: Hey, there's a first time for everything.

<<elseif $category1 == "extracurricular">>
    {$player}: Yeah, 'cus you could do so much better?
    Brad: My blind grandma could do better than that!

<<endif>>

[[You should really watch your mouth|Watch mouth]]
[[Please, just leave me alone|Leave alone]]
===
title: Watch mouth
tags: 
colorID: 0
position: 300,-692
---
{$player}: You should really watch your mouth.
<<set $mood to $mood + $picked_on>>
Brad: Oh, what are you gonna do about it?
<<show grant angry left>>
Grant: Whatever man, you're not worth it.  

[[End lunch]]
===
title: Leave alone
tags: 
colorID: 0
position: 548,-691
---
<<set $mood to $mood - $picked_on>>
{$player}: Please, just leave me alone. 
Grant: Whatever man, you're not worth it.

[[End lunch]]
===
title: End lunch
tags: 
colorID: 0
position: 300,-933
---
<<hide brad>>
<<hide grant>>
Eventually, the bullies wander away.
Juana: ...I'm sorry about that.
{$player}: It's fine.
<<hide juana>>
The rest of your lunch passes uneventfully.
<<load 8 hallway>>
===

title: Start
tags: 
colorID: 0
position: -333,451
---
<<show background suburb>>
<<show phone notif center>>
Your phone pings as you receive a text message notification.

<<if $category2 == "popularity">>
    Claire: "Hey {$player}!"
    Claire: "Did you hear all about how awesome my birthday party was last weekend?"
    <<set $mood to $mood + $picked_on>>
    Claire: "No one invited you, huh? Probably because no one likes you"

<<elseif $category2 == "partner">>
    <<show phone hands center>>
    You open the message to see a photo of your {$passion2} holding hands with someone else.
    <<set $mood to $mood + $picked_on>>
    Claire: "Looks like you weren't worth waiting for."

<<elseif $category2 == "friendships">>
    Claire: "Hey, are you coming?"
    {$player}: "Huh?"
    Claire: "Whoops, wrong person."
    {$player}: "Are you having people over?"
    Claire: "Listen, {$player}. You’re nice and all, but you're just kind of a bummer."
    <<set $mood to $mood + $picked_on>>
    Claire: "We don't want to hang out with you anymore."

<<elseif $passion2 == "academics">>
    Claire: "Hey..."
    <<set $mood to $mood + $picked_on>>
    Claire: "Is it true that you got EXTRA time for a test that you BOMBED?!"
<<elseif $passion2 == "sports">>
    Claire: "Hey..."
    <<set $mood to $mood + $picked_on>>
    Claire: "Is it true that you BLEW IT at the game last week?!
<<elseif $passion2 == "theater">>
    Claire: "Hey..."
    <<set $mood to $mood + $picked_on>>
    Claire: "Is it true that you EMBARRASSED yourself at the play last week?!"
<<elseif $passion2 == "arts">>
    Claire: "Hey..."
    <<set $mood to $mood + $picked_on>>
    Claire: "Is it true that you EMBARRASSED yourself at the art show last week?!"
<<endif>>

[[Ignore it|Ignore]]
[[Message back|Message back]]
===
title: Ignore
tags: 
colorID: 0
position: -661,231
---
<<hide phone>>
You don't honor this message with a response. You have more important things to worry about.

[[End]]
===
title: End
tags: 
colorID: 0
position: -1296,229
---
Eventually, your home appears around the corner.
You unlock the door, shrug off your backpack, and finally sit down to relax.
<<end>>
===
title: Close
tags: 
colorID: 0
position: -1307,660
---
<<hide phone>>
{$player}: What a jerk...

[[End]]
===
title: Message back
tags: 
colorID: 0
position: -654,657
---
<<if $category2 == "extracurricular">>
    {$player}: "That's none of your business"
    Claire: "So it IS true?"

<<elseif $category2 == "popularity">>
    {$player}: "Whatever, I was busy anyways"
    Claire: "Yeah, keep telling yourself that"

<<elseif $category2 == "partner">>
    {$player}: "Please stay out of my business"
    Claire: "I'M not the one you should be worried about lmao"

<<elseif $category2 == "friendships">>
    {$player}: "Whatever, goodbye"
    Claire: "Lol we WON’T miss you!"

<<endif>>

[[Just leave me alone|Close1]]
[[Screw you! I hate you!|Close2]]
[[(Block her messages)|Close3]]
===
title: Close1
tags: 
colorID: 0
position: -987,417
---
{$player}: "Just leave me alone"
<<set $mood to $mood - $picked_on>>
Claire: "Me leaving you alone in exchange for you leaving me alone? Done."
You shove your phone into your pocket.
 
[[Close]]
===
title: Close2
tags: 
colorID: 0
position: -979,659
---
{$player}: "Screw you! I hate you!"
<<set $mood to $mood + $picked_on>>
Claire: "Aww, are you gonna go cry to your friends at the nuthouse?"
Instead of responding, you shove your phone into your pocket.
[[Close]]
===
title: Close3
tags: 
colorID: 0
position: -981,930
---
<<set $mood to $mood - $picked_on>>
You block her and the messages disappear from your screen.
[[Close]]
===
